piece base, body, turret, gun, gp0, gp1, gp2, gp3, sp0, sp1, sp2, sp3;

#include "blob.h"

static-var gp, burst, b, shooting;

ChangedBlobCount(type, count, root) {
	if (type==1) {
		if (count > 150) count=150; //flighttime won't support more
		get WEAPON_RANGE(-1, [600] + count*[20]);
		//get WEAPON_PROJECTILE_SPEED(-1, ([500] + root*[30]) /32);
	}
	if (type==2) {
		burst=count + 1;
		//get WEAPON_SPRAY(-1, root*220);
		if (!shooting) b=burst;
	}
}

Create() {
	call-script ChangedBlobCount(1,0,0);
	call-script ChangedBlobCount(2,0,0);
}

ResetAim() {
	sleep 4000;
	turn gun to x-axis 0 speed <180>;
	b=burst;
	shooting=0;
}

QueryWeapon1(p) {
	p=gp0 + gp;
}

AimFromWeapon1(p) {
	p=turret;
}

AimWeapon1(h,p) {
	signal SIG_Aim;
	set-signal-mask SIG_Aim;
	start-script ResetAim();
	turn turret to y-axis h speed <300>;
	turn gun to x-axis 0 - p speed <180>;
	wait-for-turn turret around y-axis;
	wait-for-turn gun around x-axis;
	return 1;
}

Shot1() {
	if(gp==0) {
		emit-sfx 1024 from gp1;
		emit-sfx 1025 from sp1;
		gp=1;
	}
	else if(gp==1) {
		emit-sfx 1024 from gp2;
		emit-sfx 1025 from sp2;
		gp=2;
	}
	else if(gp==2) {
		emit-sfx 1024 from gp3;
		emit-sfx 1025 from sp3;
		gp=3;
	}
	else {
		emit-sfx 1024 from gp0;
		emit-sfx 1025 from sp0;
		gp=0;
	}

	if (b > 1) {
		shooting=1;
		//sleep 120;
		get WEAPON_RELOADSTATE(-1,0); //instant reload
		--b;
	} 
	else {
		shooting=0;
		b=burst;
	}
}